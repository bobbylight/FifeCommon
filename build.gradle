plugins {
	id 'java-library'
}
['java', 'checkstyle', 'maven-publish', 'signing' ].each { apply plugin: it }

group = 'com.fifesoft.rtext'
archivesBaseName = 'fife.common'

ext.isReleaseVersion = !project.version.endsWith('SNAPSHOT')

assert JavaVersion.current().isJava11Compatible()

repositories {
	mavenCentral()
}

dependencies {
	api 'org.pushing-pixels:radiance-substance:2.5.1'
	api 'org.apache.xmlgraphics:batik-rasterizer:1.12'
	testImplementation 'junit:junit:4.13'
}

wrapper {
	gradleVersion '6.2.2'
}

checkstyle {
	toolVersion = '8.30'
}

compileJava {
	sourceCompatibility javaVersion
	targetCompatibility javaVersion
	options.debug = true
	options.debugOptions.debugLevel = 'source,vars,lines'
	options.compilerArgs << '-Xlint:deprecation' << '-Xlint:unchecked'
}

ext.sharedManifest = manifest {
	attributes(
		'Specification-Title': 'FifeCommon',
		'Specification-Version': version,
		'Implementation-Title': 'org.fife.ui',
		'Implementation-Version': version)

}

java {
	withSourcesJar()
	withJavadocJar()
}
jar {
	manifest { from sharedManifest }
}



publishing {
	repositories {
		maven {
			def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
			def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
			url = isReleaseVersion ? releasesRepoUrl : snapshotsRepoUrl
			credentials { // Credentials usually kept in user's .gradle/gradle.properties
				// We must defensively check for these properties so Travis CI build works
				username = project.hasProperty('ossrhUsername') ? ossrhUsername : 'unknown'
				password = project.hasProperty('ossrhPassword') ? ossrhPassword : 'unknown'
			}
		}
	}
	publications {
		maven(MavenPublication) {

			groupId = 'com.fifesoft.rtext'
			artifactId = archivesBaseName
			version = version

			from components.java

			pom {
				name = archivesBaseName
				description = 'Common UI components and utilities for Swing applications.'
				url = 'http://www.fifesoft.com/rtext/'
				inceptionYear = '2003'
				packaging = 'jar'
				licenses {
					license {
						name = 'BSD-3-Clause'
						url = 'https://github.com/bobbylight/FifeCommon'
					}
				}
				developers {
					developer {
						name = 'Robert Futrell'
					}
				}
				scm {
					url = 'https://github.com/bobbylight/FifeCommon'
					connection = 'scm:git:git://github.com/bobbylight/FifeCommon'
					developerConnection = 'scm:git:git@github.com:bobbylight/FifeCommon'
					if (isReleaseVersion) {
						tag = project.version
					}
				}
			}
		}
	}
}

signing {
	// Don't require signing for e.g. ./gradlew install
	required { gradle.taskGraph.hasTask('publish') && isReleaseVersion }
	sign publishing.publications.maven
}
tasks.withType(Sign) {
	onlyIf { isReleaseVersion }
}
