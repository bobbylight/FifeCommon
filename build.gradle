plugins {
	id 'base'
	id 'java-library'
}
['java', 'checkstyle', 'maven-publish', 'signing' ].each { apply plugin: it }

group = 'com.fifesoft.rtext'
// Note: Local JDK 21: /Library/Java/JavaVirtualMachines/jdk-21.0.5+11/Contents/Home

base {
	archivesName = 'fife.common'
}

ext.isReleaseVersion = !project.version.endsWith('SNAPSHOT')

assert JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_21)

repositories {
	mavenCentral()
}

dependencies {
	api 'org.pushing-pixels:radiance-substance:4.5.0'
	api 'org.apache.xmlgraphics:batik-rasterizer:1.19'

	testImplementation 'org.junit.jupiter:junit-jupiter:5.13.2'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.13.2'
}

wrapper {
	gradleVersion = '8.14.2'
}

checkstyle {
	toolVersion = '10.26.1'
}

tasks.withType(Javadoc) {
	options.addStringOption('Xdoclint:none', '-quiet')
}

test {
	useJUnitPlatform()
}

compileJava {
	options.release = Integer.parseInt(javaReleaseVersion)
	options.debug = true
	options.debugOptions.debugLevel = 'source,vars,lines'
	options.compilerArgs << '-Xlint:deprecation' << '-Xlint:unchecked'
}

java {
	withSourcesJar()
	withJavadocJar()
}
jar {
	manifest {
		attributes(
			'Specification-Title': 'FifeCommon',
			'Specification-Version': version,
			'Implementation-Title': 'org.fife.ui',
			'Implementation-Version': version)

	}
}



publishing {
	repositories {
		maven {
			def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
			def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
			url = isReleaseVersion ? releasesRepoUrl : snapshotsRepoUrl
			credentials { // Credentials usually kept in user's .gradle/gradle.properties
                // We must defensively check for these properties so CI builds work
                username = project.hasProperty('ossrhToken') ? ossrhToken : 'unknown'
                password = project.hasProperty('ossrhTokenPassword') ? ossrhTokenPassword : 'unknown'
			}
		}
	}
	publications {
		maven(MavenPublication) {

			groupId = 'com.fifesoft.rtext'
			artifactId = 'fife.common'
			version = version

			from components.java

			pom {
				name = 'fife.common'
				description = 'Common UI components and utilities for Swing applications.'
				url = 'https://bobbylight.github.io/RText/'
				inceptionYear = '2003'
				packaging = 'jar'
				licenses {
					license {
						name = 'BSD-3-Clause'
						url = 'https://github.com/bobbylight/FifeCommon'
					}
				}
				developers {
					developer {
						name = 'Robert Futrell'
					}
				}
				scm {
					url = 'https://github.com/bobbylight/FifeCommon'
					connection = 'scm:git:git://github.com/bobbylight/FifeCommon'
					developerConnection = 'scm:git:git@github.com:bobbylight/FifeCommon'
					if (isReleaseVersion) {
						tag = project.version
					}
				}
			}
		}
	}
}

signing {
	// Don't require signing for e.g. ./gradlew install
	required = gradle.taskGraph.hasTask('publish') && isReleaseVersion
	sign publishing.publications.maven
}
tasks.withType(Sign) {
	onlyIf { isReleaseVersion }
}
